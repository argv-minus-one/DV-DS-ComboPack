class AetheriusHUD : BaseStatusBar {
	Array<GUIWidget> Widgets;
	
	override void Init() {
		Super.Init();
		SetSize(0, 1440, 1080);
		
		// HP meter
		HPMeter = new("AethHUDMeter");
		HPMeter.ConsoleLabel = "AetheriusHUD.HPMeter";
		HPMeter.Init(null);
		HPMeter.MainFill.DecodeColorString(HPMeterFill);
		HPMeter.OverFill.DecodeColorString(HPMeterOverfill);
		HPMeter.LengthBase = 500;
		HPMeter.LengthScale = 1;
		HPMeter.LabelText.Text = "HP";
		HPMeter.Align.X = XALIGN_LEFT;
		//HPMeter.Scale = (5, 3);
		// TODO: Coordinates relative to screen edges/corners/size
		HPMeter.TopLayout((200, Screen.GetHeight() - 190));
		Widgets.Push(HPMeter);
	}
	
	override void Tick() {
		Super.Tick();
		
		HPMeter.Value = CPlayer.Health;
		HPMeter.MaxValue = CPlayer.mo.GetMaxHealth(true);
		HPMeter.Tick();
	}
	
	override void Draw(int state, double ticFrac) {
		Super.Draw(state, ticFrac);
		
		if (dvds_newhud == 2 && (state == HUD_StatusBar || state == HUD_Fullscreen)) {
			BeginHUD();
			
			DrawCharacterPortrait(ticFrac);
			
			for (let i = 0, size = Widgets.Size(); i < size; i++)
				Widgets[i].Draw();
		}
	}
	
	// Status
	
	enum ACSStatusCondition {
		STATUS_LIGHT,
		STATUS_INVIS,
		STATUS_FLIGHT,
		STATUS_STEALTH,
		STATUS_INVUL,
		STATUS_TIMEFREEZE,
		STATUS_FEARAURA,
		STATUS_DIVINEAVATAR,
		STATUS_SPREADERAURA1,
		STATUS_SPREADERAURA2,
		STATUS_OOMPH,
		STATUS_BUFF,
		STATUS_ACCELERATE,
		STATUS_BLUNT,
		STATUS_SAP,
		STATUS_DECELERATE,
		STATUS_POISON,
		STATUS_CORROSION,
		STATUS_FATIGUE,
		STATUS_BLIND,
		STATUS_SLEEP,
		STATUS_SILENCE,
		STATUS_RADSUIT,
		STATUS_BERSERK,
		STATUS_MAX
	}
	
	int StatusDuration(ACSStatusCondition condition) {
		return GetGlobalACSArrayValue(16, condition);
	}
	
	// Character Portrait
	
	/*
	 * Dimensions for the portrait oval (from the bottom left):
	 *        │  X  │  Y
	 * ───────┼─────┼────
	 * Size   │ 171 │ 175
	 * Offset │  44 │   7
	 * Center │ 130 │  95
	 */
	
	void DrawCharacterPortrait(double ticFrac) {
		String portraitImage = "";
		
		if (CPlayer.playerstate == PST_DEAD)
			portraitImage = "PCOF1NRM";
		else if (CPlayer.cls is "DeggarisMontegger") {
			if (StatusDuration(STATUS_INVUL))
				portraitImage = "PDEG1INV";
			else
				portraitImage = "PDEG1NRM";
		}
		else if (CPlayer.cls is "IlluciaHendershot") {
			if (StatusDuration(STATUS_INVUL))
				portraitImage = "PILL1INV";
			else
				portraitImage = "PILL1NRM";
		}
		else if (CPlayer.cls is "FloraBriscoletti") {
			if (StatusDuration(STATUS_DIVINEAVATAR)) {
				if (StatusDuration(STATUS_INVUL))
					portraitImage = "PFLO2INV";
				else
					portraitImage = "PFLO2NRM";
			}
			else {
				if (StatusDuration(STATUS_INVUL))
					portraitImage = "PFLO1INV";
				else
					portraitImage = "PFLO1NRM";
			}
		}
		
		if (portraitImage != "") {
			int flags = DI_ITEM_LEFT_BOTTOM | DI_SCREEN_LEFT_BOTTOM;
			DrawImage("PORTEMPT", (0, 0), flags);
			DrawImage(portraitImage, (0, 0), flags);
		}
		else
			DrawTexture(GetMugShot(5), (130, 95), DI_ITEM_CENTER | DI_SCREEN_LEFT_BOTTOM);
		
		// TODO: Status conditions
	}
	
	// Meters
	
	AethHUDMeter HPMeter;
	
	// Dear God, what a horrible hack. The bytes of this string are ARGB8888 colors.
	const HPMeterFill = "\xFF\x54\x00\x00\xFF\x69\x00\x00\xFF\x85\x00\x00\xFF\xA2\x00\x00\xFF\xC4\x00\x00\xFF\xE9\x00\x00\xFF\xFF\x02\x03\xFF\xFF\x10\x11\xFF\xFF\x1D\x20\xFF\xFF\x2F\x32\xFF\xFF\x3B\x3D\xFF\xFF\x47\x49\xFF\xFF\x52\x54\xFF\xFF\x60\x63\xFF\xFF\x6C\x70\xFF\xFF\x7D\x80\xFF\xFF\x91\x94\xFF\xFF\xA5\xA7\xFF\xFF\xC1\xC1\xFF\xFF\xDC\xDC";
	const HPMeterOverfill = "\xFF\xFF\xF9\xF9\xFF\xFF\xF3\xF3\xFF\xFF\xED\xED\xFF\xFF\xE8\xE9\xFF\xFF\xE2\xE4\xFF\xFF\xDD\xDE\xFF\xFF\xD9\xDA\xFF\xFF\xD3\xD4\xFF\xFF\xCE\xCF\xFF\xFF\xC8\xC9\xFF\xFF\xC0\xC2\xFF\xFF\xB2\xB4\xFF\xFF\xA0\xA3\xFF\xFF\x72\x7A\xFF\xFB\x00\x00\xFF\xF4\x00\x00\xFF\xEC\x00\x00\xFF\xE5\x00\x00\xFF\xDC\x00\x00\xFF\xD4\x00\x00";
}

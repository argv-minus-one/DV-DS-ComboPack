class AethHUDMeter : GUIRibbonMeter {
	GUIEdgeGroup Frame, FrameGlow, BackgroundGlass;
	GUISwitchGroup DrawOverSwitcher;
	float FrameGlowOpacity;
	
	override void Init(GUIWidget parent) {
		Super.Init(parent);
		
		let hudFont = Font.GetFont('DQHUDFONTSML');
		NumberText.TextFont = hudFont;
		LabelText.TextFont = hudFont;
		NumberText.FontBaseline = LabelText.FontBaseline = 22;
		
		Frame = new("GUIEdgeGroup");
		Frame.ConsoleLabel = ConsoleLabel..".Frame";
		Frame.Init(self);
		Frame.UseTextureSet("SLAMFRM");
		
		FrameGlow = new("GUIEdgeGroup");
		FrameGlow.ConsoleLabel = ConsoleLabel..".FrameGlow";
		FrameGlow.Init(self);
		FrameGlow.UseTextureSet("SLAMFRG");
		
		SetFrameColors();
		
		let DrawOverStack = new("GUIStackGroup");
		DrawOverStack.ConsoleLabel = ConsoleLabel..".DrawOverStack";
		DrawOverStack.Init(self);
		DrawOverStack.Children.Resize(2);
		DrawOverStack.Children[0] = Frame;
		DrawOverStack.Children[1] = FrameGlow;
		DrawOver = DrawOverStack;
		
		BackgroundGlass = new("GUIEdgeGroup");
		BackgroundGlass.ConsoleLabel = ConsoleLabel..".BackgroundGlass";
		BackgroundGlass.Init(self);
		BackgroundGlass.UseTextureSet("SLAMBKG");
		DrawUnder = BackgroundGlass;
		
		MainFill.Skew = 1;
		OverFill.Skew = 1;
		FillInsets.Set(6, 8, 6, 8);
		NumberText.Insets.Set(6, 15, 6, 15);
	}
	
	void SetFrameColors(Color normalColor = 0xff000000, int normalPaletteColor = -1, Color criticalColor = 0xffff0000, int criticalPaletteColor = -1) {
		for (let col = 0; col < 3; col++)
		for (let row = 0; row < 3; row++) {
			let nw = Frame.Children[col][row], cw = FrameGlow.Children[col][row];
			
			if (nw is "GUIImage") {
				GUIImage(nw).AlphaBlend = true;
				GUIImage(nw).AlphaBlendColor.Set(normalColor, normalPaletteColor);
			}
			
			if (cw is "GUIImage") {
				GUIImage(cw).AlphaBlend = true;
				GUIImage(cw).AlphaBlendColor.Set(criticalColor, criticalPaletteColor);
			}
		}
	}
	
	private Array<GUIImage> FrameGlowImages;
	
	override void Layout() {
		Super.Layout();
		
		FrameGlowImages.Clear();
		for (let col = 0; col < 3; col++)
		for (let row = 0; row < 3; row++)
		if (FrameGlow.Children[col][row] is "GUIImage")
			FrameGlowImages.Push(GUIImage(FrameGlow.Children[col][row]));
	}
	
	override void Draw() {
		for (let i = 0, l = FrameGlowImages.Size(); i < l; i++)
			FrameGlowImages[i].Opacity = FrameGlowOpacity;
		
		Super.Draw();
	}
}

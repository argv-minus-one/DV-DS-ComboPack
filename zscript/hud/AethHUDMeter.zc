class AethHUDMeter : GUIRibbonMeter {
	GUIEdgeGroup NormalFrame, CriticalFrame, BackgroundGlass;
	double CriticalThreshold;
	bool CriticalIsHigh;
	private GUISwitchGroup DrawOverSwitcher;
	
	override void Init(GUIWidget parent) {
		Super.Init(parent);
		
		let hudFont = Font.GetFont('DQHUDFONTSML');
		NumberText.TextFont = hudFont;
		LabelText.TextFont = hudFont;
		NumberText.FontBaseline = LabelText.FontBaseline = 22;
		
		CriticalThreshold = .25;
		NormalFrame = new("GUIEdgeGroup");
		NormalFrame.ConsoleLabel = ConsoleLabel..".NormalFrame";
		NormalFrame.Init(self);
		NormalFrame.UseTextureSet("SLAMFRN");
		
		CriticalFrame = new("GUIEdgeGroup");
		CriticalFrame.ConsoleLabel = ConsoleLabel..".CriticalFrame";
		CriticalFrame.Init(self);
		CriticalFrame.UseTextureSet("SLAMFRC");
		
		SetFrameColors();
		
		DrawOverSwitcher = new("GUISwitchGroup");
		DrawOverSwitcher.ConsoleLabel = ConsoleLabel..".DrawOverSwitcher";
		DrawOverSwitcher.Init(self);
		DrawOverSwitcher.Children.Resize(2);
		DrawOverSwitcher.Children[0] = NormalFrame;
		DrawOverSwitcher.Children[1] = CriticalFrame;
		DrawOverSwitcher.CurrentWidget = NormalFrame;
		DrawOver = DrawOverSwitcher;
		
		BackgroundGlass = new("GUIEdgeGroup");
		BackgroundGlass.ConsoleLabel = ConsoleLabel..".BackgroundGlass";
		BackgroundGlass.Init(self);
		BackgroundGlass.UseTextureSet("SLAMBGN");
		DrawUnder = BackgroundGlass;
		
		MainFill.Skew = 1;
		OverFill.Skew = 1;
		FillInsets.Set(6, 8, 6, 8);
		NumberText.Insets.Set(6, 15, 6, 15);
	}
	
	void SetFrameColors(Color normalColor = 0xff000000, int normalPaletteColor = -1, Color criticalColor = 0xffff0000, int criticalPaletteColor = -1) {
		for (let col = 0; col < 3; col++)
		for (let row = 0; row < 3; row++) {
			let nw = NormalFrame.Children[col][row], cw = CriticalFrame.Children[col][row];
			
			if (nw is "GUIImage") {
				GUIImage(nw).AlphaBlend = true;
				GUIImage(nw).AlphaBlendColor.Set(normalColor, normalPaletteColor);
			}
			
			if (cw is "GUIImage") {
				GUIImage(cw).AlphaBlend = true;
				GUIImage(cw).AlphaBlendColor.Set(criticalColor, criticalPaletteColor);
			}
		}
	}
	
	override void Draw() {
		let vp = double(ValueInterp.GetValue()) / double(MaxValueInterp.GetValue());
		
		DrawOverSwitcher.CurrentWidget =
			(CriticalIsHigh && vp > CriticalThreshold) || (!CriticalIsHigh && vp < CriticalThreshold)?
			CriticalFrame : NormalFrame;
		
		Super.Draw();
	}
}

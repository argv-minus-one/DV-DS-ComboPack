/**
 * A widget that contains an array of other widgets, and draws them on top of each other. The first widget in the array is drawn on the bottom of the stack; the last widget is drawn on top.
 *
 * Child widgets align themselves in the space according to their own Align field, not that of the GUIStackGroup. Thus, a single GUIStackGroup might contain one widget in its top-left corner and another widget in its bottom-right.
 */
class GUIStackGroup : GUIArrayGroup {
	override double PreferredWidth(double height = -1.) {
		return ChildrenMaxPreferredWidth(height) + Insets.X(EffXScale());
	}
	
	override double PreferredHeight(double width = -1.) {
		return ChildrenMaxPreferredHeight(width) + Insets.Y(EffYScale());
	}
	
	override void Layout() {
		Super.Layout();
		
		Rectangle ca;
		GetContentArea(ca);
		
		for (let i = 0, l = Children.Size(); i < l; i++) {
			Children[i].Bounds.Copy(ca);
			Children[i].Layout();
		}
	}
}

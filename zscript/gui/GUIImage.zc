/**
 * Draws a texture.
 */
class GUIImage : GUIWidget {
	TextureID Image;
	bool PreserveAspectRatio;
	Rectangle ImageDrawArea;
	
	/**
	 * If set to true, the image is not drawn directly. Instead, it is used as an alpha channel, and AlphaBlendColor is filled into the draw area.
	 */
	bool AlphaBlend;
	
	/** Blend color to use if AlphaBlend is true. */
	PalettedColor AlphaBlendColor;
	
	override void Init(GUIWidget parent = null) {
		Super.Init(parent);
		PreserveAspectRatio = true;
	}
	
	double ImageAspectRatio() {
		if (Image.IsValid()) {
			let s = ImageSize();
			return s.X / s.Y;
		}
		else
			return 1.;
	}
	
	override EResizeBias ResizeBias() {
		if (PreserveAspectRatio && Image.IsValid()) {
			// Images' resize bias is along their short axis. That is, the size of the short axis depends on the size of the long axis.
			let s = ImageSize();
			if (s.Y < s.X)
				return RBIAS_VERTICAL;
			else
				return RBIAS_HORIZONTAL;
		}
		else
			return RBIAS_NONE;
	}
	
	Vector2 ImageSize() {
		return TexMan.GetScaledSize(Image);
	}
	
	override double PreferredWidth(double height = -1.) {
		if (Image.IsValid()) {
			if (height > 0. && ResizeBias() == RBIAS_VERTICAL)
				return height * ImageAspectRatio();
			else {
				let sts = ImageSize();
				return sts.X * Scale.X;
			}
		}
		else
			return 0.;
	}
	
	override double PreferredHeight(double width = -1.) {
		if (Image.IsValid()) {
			if (width > 0. && ResizeBias() == RBIAS_HORIZONTAL)
				return width / ImageAspectRatio();
			else {
				let sts = ImageSize();
				return sts.Y * Scale.Y;
			}
		}
		else
			return 0.;
	}
	
	override void Layout() {
		Super.Layout();
		
		Rectangle ca;
		ca.Size = DC.Bounds;
		
		ImageDrawArea.Size = PreserveAspectRatio? PreferredSize(ca.Size) : ca.Size;
		ImageDrawArea.PositionIn(ca, Align.X, Align.Y);
		
		if (zsgui_debug_imagewidget_layout) {
			Rectangle cas;
			cas.Copy(ca);
			DC.ToScreenRect(cas);
			Console.Printf("%s will draw its image in %s (screen coordinates: %s).", ToString(), ImageDrawArea.ToString(), cas.ToString());
		}
	}
	
	override void Draw() {
		Super.Draw();
		
		if (Image.IsValid()) {
			if (AlphaBlend)
				DC.DrawAlphaFill(ImageDrawArea, AlphaBlendColor, Image);
			else
				DC.DrawTexture(ImageDrawArea, Image);
		}
	}
}

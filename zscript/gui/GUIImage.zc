/**
 * Draws a texture.
 */
class GUIImage : GUIWidget {
	TextureID Image;
	bool PreserveAspectRatio;
	float Opacity;
	Rectangle ImageDrawArea;
	
	/**
	 * Factor by which the preferred size of this GUIImage will be multiplied. Defaults to 1. Negative values are treated as 0.
	 */
	double PreferredSizeFactor;
	
	/**
	 * If set to true, the image is not drawn directly. Instead, it is used as an alpha channel, and AlphaBlendColor is filled into the draw area.
	 */
	bool AlphaBlend;
	
	/** Blend color to use if AlphaBlend is true. */
	PalettedColor AlphaBlendColor;
	
	override void Init(GUIWidget parent = null) {
		Super.Init(parent);
		PreserveAspectRatio = true;
		Opacity = PreferredSizeFactor = 1;
	}
	
	double ImageAspectRatio() {
		if (Image.IsValid()) {
			let s = ImageSize();
			return s.X / s.Y;
		}
		else
			return 1.;
	}
	
	override EResizeBias ResizeBias() {
		if (PreserveAspectRatio && Image.IsValid()) {
			// Images' resize bias is along their short axis. That is, the size of the short axis depends on the size of the long axis.
			let s = ImageSize();
			if (s.Y < s.X)
				return RBIAS_VERTICAL;
			else
				return RBIAS_HORIZONTAL;
		}
		else
			return RBIAS_NONE;
	}
	
	Vector2 ImageSize() {
		return TexMan.GetScaledSize(Image);
	}
	
	override double PreferredWidth(double height = -1.) {
		if (Image.IsValid()) {
			if (height > 0. && ResizeBias() == RBIAS_VERTICAL)
				return height * ImageAspectRatio();
			else {
				let s = ImageSize();
				return s.X * max(PreferredSizeFactor, 0.);
			}
		}
		else
			return 0.;
	}
	
	override double PreferredHeight(double width = -1.) {
		if (Image.IsValid()) {
			if (width > 0. && ResizeBias() == RBIAS_HORIZONTAL)
				return width / ImageAspectRatio();
			else {
				let s = ImageSize();
				return s.Y * max(PreferredSizeFactor, 0.);
			}
		}
		else
			return 0.;
	}
	
	override void Layout() {
		Super.Layout();
		
		Rectangle ca;
		GetContentArea(ca);
		
		ImageDrawArea.Size = PreserveAspectRatio? PreferredSize(ca.Size) : ca.Size;
		ImageDrawArea.PositionIn(ca, Align.X, Align.Y);
		
		if (zsgui_debug_imagewidget_layout)
			Console.Printf("%s will draw its image in %s.", ToString(), ImageDrawArea.ToString());
	}
	
	override void Draw() {
		Super.Draw();
		
		if (Image.IsValid())
			Screen.DrawTexture(
				Image,
				false,
				ImageDrawArea.Origin.X,
				ImageDrawArea.Origin.Y,
				DTA_DestWidthF, ImageDrawArea.Size.X,
				DTA_DestHeightF, ImageDrawArea.Size.Y,
				DTA_FillColor, AlphaBlendColor.DTAColor,
				DTA_AlphaChannel, AlphaBlend,
				DTA_Alpha, Opacity
			);
	}
}

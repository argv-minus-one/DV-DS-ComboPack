/**
 * An all-in-one meter widget that uses GUIColorRibbon for fill, GUIText for labeling, and arbitrary widgets drawn under/over the fill part.
 */
class GUIRibbonMeter : GUIWidget {
	DynamicValueInterpolator ValueInterp, MaxValueInterp;
	int Value, MaxValue;
	GUIText LabelText, NumberText;
	GUIColorRibbon MainFill, OverFill;
	bool DrawValue, DrawMaxValue;
	GUIInsets FillInsets;
	GUIWidget DrawUnder, DrawOver;
	
	/** If LengthScale is set to a positive value, then the length of the meter is MaxValue * LengthScale + LengthAdd, and the LengthBase field is the maximum length instead. */
	double LengthScale;
	double LengthBase;
	double LengthAdd;
	
	override void Init(GUIWidget parent = null) {
		Super.Init(parent);
		
		LabelText = new("GUIText");
		LabelText.ConsoleLabel = ConsoleLabel..".LabelText";
		LabelText.Init(self);
		
		NumberText = new("GUIText");
		NumberText.ConsoleLabel = ConsoleLabel..".NumberText";
		NumberText.Init(self);
		NumberText.PrefLineLength = 7;
		
		MainFill = new("GUIColorRibbon");
		MainFill.ConsoleLabel = ConsoleLabel..".MainFill";
		MainFill.Init(self);
		MainFill.Align.X = XALIGN_LEFT;
		
		OverFill = new("GUIColorRibbon");
		OverFill.ConsoleLabel = ConsoleLabel..".OverFill";
		OverFill.Init(self);
		OverFill.Align.X = XALIGN_LEFT;
		
		DrawMaxValue = DrawValue = true;
		ValueInterp = DynamicValueInterpolator.Create(0, 0.25, 1, 8);
		MaxValueInterp = DynamicValueInterpolator.Create(0, 0.15, 1, 6);
		LengthBase = 200;
		
		WStack = new("GUIStackGroup");
		WStack.ConsoleLabel = ConsoleLabel..".WStack";
		WStack.Init(self);
		
		WSeq = new("GUISeqGroup");
		WSeq.ConsoleLabel = ConsoleLabel..".WSeq";
		WSeq.Init(self);
	}
	
	void SetBackgroundTextureSet(String textureNamePrefix, int texUseType) {
		DrawUnder = new("GUIEdgeGroup");
		DrawUnder.ConsoleLabel = ConsoleLabel..".DrawUnder";
		DrawUnder.Init(self);
		GUIEdgeGroup(DrawUnder).UseTextureSet(textureNamePrefix, texUseType);
		RequestLayout();
	}
	
	void SetForegroundTextureSet(String textureNamePrefix, int texUseType) {
		DrawOver = new("GUIEdgeGroup");
		DrawOver.ConsoleLabel = ConsoleLabel..".DrawOver";
		DrawOver.Init(self);
		GUIEdgeGroup(DrawOver).UseTextureSet(textureNamePrefix, texUseType);
		RequestLayout();
	}
	
	void Tick() {
		ValueInterp.Update(Value);
		
		let oldMaxValueInterp = MaxValueInterp.GetValue();
		MaxValueInterp.Update(MaxValue);
		
		// If the whole meter needs to grow/shrink, then we need to recompute layout.
		if (oldMaxValueInterp != MaxValueInterp.GetValue() && LengthScale > 0.)
			RequestLayout();
	}
	
	override double PreferredWidth(double height = -1.) {
		SetupTree();
		return WSeq.PreferredWidth(height) + Insets.X();
	}
	
	override double PreferredHeight(double width = -1.) {
		SetupTree();
		return WSeq.PreferredHeight(width) + Insets.Y();
	}
	
	private double GetFillFraction(double adjust = 0.) {
		let vi = ValueInterp.GetValue(), mvi = MaxValueInterp.GetValue();
		double rf;
		
		if (mvi > 0)
			rf = double(vi) / double(mvi);
		else
			rf = 1;
		
		return clamp(rf + adjust, 0., 1.);
	}
	
	private GUIStackGroup WStack;
	private GUISeqGroup WSeq;
	private bool TreeSetupDone;
	
	private void SetupTree() {
		if (TreeSetupDone)
			return;
		
		WSeq.ReverseOrder = Align.X == XALIGN_RIGHT;
		WSeq.Children.Clear();
		
		WStack.Children.Clear();
		
		if (DrawUnder) {
			WStack.Children.Push(DrawUnder);
			DrawUnder.ConsoleLabel = ConsoleLabel..".DrawUnder";
		}
		
		if (MainFill) {
			MainFill.Align.Copy(Align);
			MainFill.Insets.Copy(FillInsets);
			WStack.Children.Push(MainFill);
			MainFill.ConsoleLabel = ConsoleLabel..".MainFill";
		}
		
		if (OverFill) {
			OverFill.Align.Copy(Align);
			OverFill.Insets.Copy(FillInsets);
			WStack.Children.Push(OverFill);
			OverFill.ConsoleLabel = ConsoleLabel..".OverFill";
		}
		
		if (DrawOver) {
			WStack.Children.Push(DrawOver);
			DrawOver.ConsoleLabel = ConsoleLabel..".DrawOver";
		}
		
		if (NumberText && DrawValue) {
			WStack.Children.Push(NumberText);
			NumberText.ConsoleLabel = ConsoleLabel..".NumberText";
		}
		
		if (LabelText) {
			WSeq.Children.Push(LabelText);
			WSeq.SpaceBetween = LabelText.TextFont.GetCharWidth(48);
			LabelText.ConsoleLabel = ConsoleLabel..".LabelText";
		}
		else
			WSeq.SpaceBetween = 0;
		
		WSeq.Children.Push(WStack);
		
		TreeSetupDone = true;
	}
	
	override void RequestLayout() {
		Super.RequestLayout();
		TreeSetupDone = false;
	}
	
	override void Layout() {
		Super.Layout();
		
		Rectangle ca;
		GetContentArea(ca);
		
		if (zsgui_debug_ribbonmeter_layout)
			Console.Printf(">>> ZsGUI: Laying out %s into area %s.", ToString(), ca.ToString());
		
		SetupTree();
		
		LayoutChild(WSeq, ca);
	}
	
	override void Draw() {
		Super.Draw();
		
		if (MainFill)
			MainFill.DrawFraction = GetFillFraction();
		if (OverFill)
			OverFill.DrawFraction = GetFillFraction(-1);
		
		if (NumberText != null && DrawValue) {
			String newText;
			
			if (DrawMaxValue)
				newText = String.Format("%d/%d", ValueInterp.GetValue(), MaxValueInterp.GetValue());
			else
				newText = String.Format("%d", ValueInterp.GetValue());
			
			if (newText != NumberText.Text) {
				NumberText.Text = newText;
				NumberText.Layout();
			}
		}
		
		WSeq.Draw();
	}
}

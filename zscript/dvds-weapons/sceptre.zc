/*
TODO:
• Make it possible to cast spells through the shield.
• Implement the spin attack.
• Make all moves cost stamina.
• Make twirl also cost MP.
• Apply damage modifiers: berserk, debuffs, strength stat, etc.
• Port the FloraWeapon class to ZScript, then derive this class from it.

When done:
• Rename to SceptreOfEmpyrea.
• Remove ACS function “SceptreTwirlFrameCheck”.
• Remove allocation of ACS miscellaneous variables 198 and 397.
*/

#include "zscript/dvds-weapons/sceptre/aerial-attack.zc"
#include "zscript/dvds-weapons/sceptre/attack.zc"
#include "zscript/dvds-weapons/sceptre/defend.zc"
#include "zscript/dvds-weapons/sceptre/puff.zc"
#include "zscript/dvds-weapons/sceptre/selecting.zc"
#include "zscript/dvds-weapons/sceptre/spin-attack.zc"
#include "zscript/dvds-weapons/sceptre/swing.zc"
#include "zscript/dvds-weapons/sceptre/twirl.zc"

// This needs to go last, since it modifies the other includes above.
#include "zscript/dvds-weapons/sceptre/constants.zc"

class SceptreOfEmpyreaZSC : AetheriusBaseWeaponZSC //FloraWeapon //32221
{
	default
	{
		Tag "Sceptre of Empyrea";
		Scale 0.5;
		Weapon.SlotNumber 1;
		Weapon.SlotPriority 1;
		Weapon.KickBack 256;
		DamageType 'Sceptre';
		+SPECTRAL;
		+WEAPON.NOAUTOAIM;
		+WEAPON.NOALERT;
		+WEAPON.MELEEWEAPON;
		Inventory.PickupMessage "What a sight... Simplistic in its majesty...";
		Obituary "%o was smacked out of the ballpark by %k's \cjSceptre of Empyrea\c-.";
	}
	
	// Code //
	
	protected int SceptreLevel, CharLevel;
	
	override void Tick()
	{
		if (Owner && Owner.player && Owner.player.ReadyWeapon == self)
		{
			SceptreLevel = CallACS("GetSceptreLevel");
			CharLevel = CallACS("GetPlayerLevel");
		}
		else
			SceptreLevel = CharLevel = 0;
		
		Super.Tick();
	}
	
	override float AnimSpeedFactor()
	{
		// If you try to just return SceptreAttackSpeeds[SceptreLevel], you'll cause an error. Odd.
		let f = SceptreAttackSpeeds[SceptreLevel];
		return f;
	}
	
	states
	{
		Spawn:
			SERP C -1 nodelay
			{
				if (
					!multiplayer &&
					playeringame[0] &&
					(
						!(players[0].mo is "FloraBriscoletti") ||
						players[0].mo.CountInv("SceptreOfEmpyrea")
					)
				)
					Destroy();
			}
			stop;
		
		Select:
			TNT1 A 1
			{
				A_SceptreBeginRaise();
				A_SetCrosshair(8);
				SetInventory("DrawingDiarisBow", 0);
				invoker.AnimSpeedsEnabled = true;
			}
			
			SCEM A 1 A_SceptreRaise("Ready");
			wait;
		
		Deselect:
			TNT1 A 0
			{
				A_SetCrosshair(0);
				CallACS("MiscVarSetDECORATE",397,0);
			}
			
			SCEM A 1 A_SceptreLower(switchWeaponWhenDone: true);
			wait;
		
		Ready:
			TNT1 A 0
			{
				invoker.ActorsHitThisSwing.Clear();
				A_SceptreRaiseReset();
				
				if (invoker.Twirling)
				{
					invoker.Twirling.Destroy();
					invoker.Twirling = null;
				}
			}
			
			SCEM A 1
			{
				invoker.bMeleeWeapon = random(1,100) <= 95;
				A_WeaponReady(WRF_ALLOWUSER1);
			}
			wait;
	}
}

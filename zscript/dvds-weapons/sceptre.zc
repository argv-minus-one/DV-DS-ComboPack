class SceptreOfEmpyreaZSC : AetheriusBaseWeaponZSC //FloraWeapon //32221
{
	default
	{
		Tag "Sceptre of Empyrea";
		Scale 0.5;
		Weapon.SlotNumber 1;
		Weapon.SlotPriority 1;
		Weapon.KickBack 256;
		+SPECTRAL;
		+WEAPON.NOAUTOAIM;
		+WEAPON.NOALERT;
		+WEAPON.MELEEWEAPON;
		Inventory.PickupMessage "What a sight... Simplistic in It's Majesty...";
		Obituary "%o was smacked out of the ballpark by %k's \cjSceptre of Empyrea\c-.";
	}
	
	// Level tables //
	
	static const float SceptreAttackSpeeds[] =
	{
		 0,       -0.03125, -0.0625,  -0.09375, -0.125,
		-0.15625, -0.1875,  -0.21875, -0.25,    -0.28125,
		-0.3125,  -0.34375, -0.375,   -0.40625, -0.4375,
		-0.46875, -0.5
	};
	
	static const uint16 SceptreDamageMin[] =
	{
		275, 280, 290,  320,  340, 365,
		425, 425, 545,  590,  640, 750,
		810, 875, 1015, 1015, 1255
	};
	
	static const uint16 SceptreDamageMax[] =
	{
		325, 330,  340,  370,  390, 415,
		475, 475,  595,  640,  690, 800,
		860, 925, 1065, 1065, 1305
	};
	
	static const uint8 SceptreRange[] =
	{
		130, 133, 135, 138, 140, 145,
		148, 153, 160, 165, 168, 170,
		173, 178, 180, 185, 192
	};
	
	static const uint16 SceptreSwingStaminaCost[] =
	{
		575, 568, 561, 554, 546, 538,
		530, 521, 512, 503, 493, 483,
		473, 462, 451, 440, 428
	};
	
	static const Sound SceptreSwingSounds[] =
	{
		 "SceptreSwingLvl0",  "SceptreSwingLvl1",  "SceptreSwingLvl2",
		 "SceptreSwingLvl3",  "SceptreSwingLvl4",  "SceptreSwingLvl5",
		 "SceptreSwingLvl6",  "SceptreSwingLvl7",  "SceptreSwingLvl8",
		 "SceptreSwingLvl9", "SceptreSwingLvl10", "SceptreSwingLvl11",
		"SceptreSwingLvl12", "SceptreSwingLvl13", "SceptreSwingLvl14",
		"SceptreSwingLvl15", "SceptreSwingLvl16"
	};
	
	static const Sound SceptreSpinAttackSounds[] =
	{
		 "SceptreSwingLvl0",  "SceptreSwingLvl1",  "SceptreSwingLvl2",
		 "SceptreSwingLvl3",  "SceptreSwingLvl4",  "SceptreSwingLvl5",
		 "SceptreSwingLvl6",  "SceptreSwingLvl7",  "SceptreSwingLvl8",
		 "SceptreSwingLvl9", "SceptreSwingLvl10", "SceptreSwingLvl11",
		"SceptreSwingLvl12", "SceptreSwingLvl13", "SceptreSwingLvl14",
		"SceptreSwingLvl15", "SceptreSwingLvl16"
	};
	
	static const uint16 SceptreAerialAttackStaminaCost[] =
	{
		575, 568, 561, 554, 546, 538,
		530, 521, 512, 503, 493, 483,
		473, 462, 451, 440, 428
	};
	
	static const uint16 SceptreSpinAttackStaminaCost[] =
	{
		1005, 996, 987, 978, 969, 960,
		 951, 942, 933, 924, 915, 903,
		 891, 879, 867, 855, 840
	};
	
	static const uint16 SceptreTwirlStaminaCost[] =
	{
		// TODO
		1, 1, 1, 1, 1, 1,
		1, 1, 1, 1, 1, 1,
		1, 1, 1, 1
	};
	
	static const uint16 SceptreTwirlMPCost[] =
	{
		// TODO
		1, 1, 1, 1, 1, 1,
		1, 1, 1, 1, 1, 1,
		1, 1, 1, 1
	};
	
	const AerialAttackButtons = BT_JUMP;
	const AerialAttackMinCharLevel = 13;
	const AerialAttackMinSceptreLevel = 4;
	const SpinAttackButtons = BT_MOVELEFT | BT_MOVERIGHT;
	const SwingDefenseDuration = 12;
	const MaxHitAngle = 120.;
	
	// Code //
	
	protected int SceptreLevel, CharLevel;
	
	override void Tick()
	{
		if (Owner && Owner.player && Owner.player.ReadyWeapon == self)
		{
			SceptreLevel = CallACS("GetSceptreLevel");
			CharLevel = CallACS("GetPlayerLevel");
		}
		else
			SceptreLevel = CharLevel = 0;
		
		Super.Tick();
	}
	
	override float AnimSpeedFactor()
	{
		// If you try to just return SceptreAttackSpeeds[SceptreLevel], you'll cause an error. Odd.
		let f = SceptreAttackSpeeds[SceptreLevel];
		return f;
	}
	
	Array<Actor> ActorsHitThisSwing;
	
	action void A_SceptreAttack(
		int range = -1,
		int damageMin = -1,
		int damageMax = -1,
		Class<Actor> puffType = "SceptrePuff"
	) {
		if (range == -1)
			range = invoker.SceptreRange[invoker.SceptreLevel];
		if (damageMin == -1)
			damageMin = invoker.SceptreDamageMin[invoker.SceptreLevel];
		if (damageMax == -1)
			damageMax = invoker.SceptreDamageMax[invoker.SceptreLevel];
		
		class<Actor> replacedPuffType = GetReplacement(puffType);
		let puffDefaults = GetDefaultByType(replacedPuffType);
		
		let it = ThinkerIterator.Create("Actor");
		for (let mo = Actor(it.Next()); mo; mo = Actor(it.Next()))
		{
			if (
				mo == self || mo.bDormant || (
					puffDefaults.bAllowThruFlags &&
					(
						puffDefaults.bThruActors ||
						(puffDefaults.bMThruSpecies && puffDefaults.Species == mo.Species)
					)
				) ||
				(puffDefaults.bThruGhost && mo.bGhost) ||
				(mo.bSpectral && !puffDefaults.bSpectral) ||
				!mo.bShootable ||
				Distance2D(mo) > range ||
				(mo.CurSector.PortalGroup != CurSector.PortalGroup && !CheckSight(mo))
			)
				continue;
			
			let angle = AngleTo(mo);
			if (abs(angle) > MaxHitAngle)
				continue;
			
			if (invoker.ActorsHitThisSwing.Find(mo) < invoker.ActorsHitThisSwing.Size())
				continue;
			else
				invoker.ActorsHitThisSwing.Push(mo);
			
			Actor puff;
			{
				let puffAngle = angle - 180.;
				let puffPos = mo.Vec3Offset(
					(mo.radius + 1) * cos(puffAngle),
					(mo.radius + 1) * sin(puffAngle),
					(mo.Height / 2) - mo.Floorclip
				);
				puff = SpawnPuff(replacedPuffType, puffPos, puffAngle, puffAngle, 0, victim: mo);
			}
			
			mo.DamageMobj(puff, self, random(damageMin, damageMax), invoker.DamageType, angle: puff.Angle);
		}
	}
	
	action void A_SceptreDefend()
	{
		// TODO
	}
	
	bool CheckSpinAttack()
	{
		return
			Owner && Owner.player &&
			(Owner.player.cmd.buttons & SpinAttackButtons) == SpinAttackButtons &&
			CallACS("GetMiscellaneousVar", 244);
	}
	
	bool CheckAerialAttack()
	{
		return
			CharLevel >= AerialAttackMinCharLevel &&
			SceptreLevel >= AerialAttackMinSceptreLevel &&
			Owner && Owner.player &&
			Owner.Vel.Z < 0. &&
			(Owner.player.cmd.buttons & AerialAttackButtons) == AerialAttackButtons;
	}
	
	states
	{
		Spawn:
			SERP C -1 nodelay
			{
				if (
					!multiplayer &&
					playeringame[0] &&
					(
						!(players[0].mo is "FloraBriscoletti") ||
						players[0].mo.CountInv("SceptreOfEmpyrea")
					)
				)
					Destroy();
			}
			stop;
		
		Select:
			TNT1 A 0
			{
				A_SetCrosshair(8);
				SetInventory("DrawingDiarisBow", 0);
				invoker.AnimSpeedsEnabled = true;
			}
			SCEM A 1 A_Raise(2);
			wait;
		
		Deselect:
			TNT1 A 0
			{
				A_SetCrosshair(0);
				CallACS("MiscVarSetDECORATE",397,0);
			}
			SCEM A 1 A_Lower(2);
			wait;
		
		Ready:
			TNT1 A 0
			{
				invoker.ActorsHitThisSwing.Clear();
			}
			
			SCEM A 1
			{
				invoker.bMeleeWeapon = random(1,100) <= 95;
				A_WeaponReady(WRF_ALLOWUSER1);
			}
			wait;
		
		Fire:
			TNT1 A 0
			{
				if (!invoker.CanOwnerMove())
					return ResolveState("Ready");
				else if (invoker.CheckAerialAttack())
					return ResolveState("AerialAttack");
				else if (invoker.CheckSpinAttack())
					return ResolveState("SpinAttack");
				else
					return ResolveState(null);
			}
			// Fall through.
		
		SwingAttack:
			SCL1 ABCDEFGHI 1;
			// Fall through.
		
		Hold:
			SCL1 J 1;
			
			TNT1 A 0
			{
				let inMenu = CallACS("GetMiscellaneousVar", 2500);
				let asleep = CallACS("GetBuffTimer", 20);
				
				if (!invoker.CanOwnerMove())
					return ResolveState("Hold");
				else if (player.cmd.buttons & BT_USER1)
					return ResolveState("User1");
				else if (invoker.CheckSpinAttack())
					return ResolveState("SpinAttack");
				else if (player.cmd.buttons & BT_ALTATTACK)
					return ResolveState("HoldCancel");
				else if (player.cmd.buttons & BT_ATTACK)
					return ResolveState("Hold");
				else
					return ResolveState(null);
			}
			// Fall through.
		
		Swing:
			TNT1 A 0
			{
				if (!CallACS("UseStaminaAttack", invoker.SceptreSwingStaminaCost[invoker.SceptreLevel]))
					return ResolveState("HoldCancel");
				
				CallACS("AddSceptreEXP", 0, 1, 1);
				A_Kiai(63);
				A_PlaySound(invoker.SceptreSwingSounds[invoker.SceptreLevel], CHAN_WEAPON);
				
				return ResolveState(null);
			}
			SCEM DEFG 1 A_SceptreDefend;
			
			SCEM H 1 bright
			{
				A_SceptreDefend();
				A_SceptreAttack(
					invoker.SceptreRange[invoker.SceptreLevel],
					invoker.SceptreDamageMin[invoker.SceptreLevel],
					invoker.SceptreDamageMax[invoker.SceptreLevel],
					"SceptrePuff"
				);
				
				// Strife mode makes the punch work like the Punch Dagger where it NEVER makes sound
				if (!(gameinfo.gametype & GAME_Strife) && (random() & 255) >= 252)
					// Sometimes a swing will be enough to wake up all monsters in the area.
					A_AlertMonsters();
				
				CallACS("StartTensionCountDown");
			}
			
			SCEM IJKLMNO 1 A_SceptreDefend;
			// Fall through.
		
		HoldCancel:
			SCL1 JIHGFEDCB 1;
			goto Ready;
		
		SpinAttack:
			// TODO
			/*
			TNT1 A 0
			{
				if (!CallACS("UseStaminaAttack", SceptreSpinAttackStaminaCost[SceptreLevel]))
					return ResolveState("Ready");
				
				// Strife mode makes the punch work like the Punch Dagger where it NEVER makes sound
				if (!(gameinfo.gametype & GAME_Strife) && (random() & 255) >= 248)
					// Sometimes a swing will be enough to wake up all monsters in the area.
					A_AlertMonsters();
				
				A_Kiai(31);
			}
			*/
			goto SwingAttack;
			
		AerialAttack:
			goto SwingAttack;
		
		AltFire:
			FLHS A 1;
			FLHS A 0 A_ReFire;
			goto Ready;
	}
}

class SceptrePuff : AetheriusMeleePuff
{
	default
	{
		VSpeed 0;
		Scale 0.375;
		Alpha 1;
		RenderStyle 'Translucent';
		Species 'Players';
		DamageType 'Sceptre';
		+PUFFONACTORS;
		+FORCEPAIN;
		+THRUSPECIES;
		+MTHRUSPECIES;
		+SPECTRAL;
	}
	
	// The following tables were generated by tools/generate-sceptre-constants.zc.
	static const Sound HitThingSounds[] =
	{
		"SceptreHitThing0", "SceptreHitThing0", "SceptreHitThing0", "SceptreHitThing0",
		"SceptreHitThing0", "SceptreHitThing0", "SceptreHitThing0", "SceptreHitThing0",
		"SceptreHitThing1", "SceptreHitThing1", "SceptreHitThing1", "SceptreHitThing1",
		"SceptreHitThing1", "SceptreHitThing1", "SceptreHitThing1", "SceptreHitThing1",
		"SceptreHitThing2"
	};
	
	static const Sound HitWallSounds[] =
	{
		"SceptreEmpyrea/HitWall", "SceptreEmpyrea/HitWall", "SceptreEmpyrea/HitWall",
		"SceptreEmpyrea/HitWall", "SceptreEmpyrea/HitWall", "SceptreEmpyrea/HitWall",
		"SceptreEmpyrea/HitWall", "SceptreEmpyrea/HitWall", "SceptreEmpyrea/HitWall1",
		"SceptreEmpyrea/HitWall1", "SceptreEmpyrea/HitWall1", "SceptreEmpyrea/HitWall1",
		"SceptreEmpyrea/HitWall1", "SceptreEmpyrea/HitWall1", "SceptreEmpyrea/HitWall1",
		"SceptreEmpyrea/HitWall1", "SceptreEmpyrea/HitWall2"
	};
	
	static const int QuakeBaseIntensity[] =
	{
		1, 1, 1, 1, 1, 1,
		1, 1, 2, 2, 2, 2,
		2, 2, 2, 2, 3
	};
	
	static const int QuakeBaseDuration[] =
	{
		8, 8, 9, 9, 10, 10,
		11, 11, 12, 12, 13, 13,
		14, 14, 15, 15, 16
	};
	
	static const int QuakeBaseRadius[] =
	{
		512, 544, 576, 608, 640, 672,
		704, 736, 768, 800, 832, 864,
		896, 928, 960, 992, 1024
	};
	
	static const float Scales[] =
	{
		0.375, 0.40625, 0.4375, 0.46875, 0.5, 0.53125,
		0.5625, 0.59375, 0.625, 0.65625, 0.6875, 0.71875,
		0.75, 0.78125, 0.8125, 0.84375, 0.875
	};
	
	static const float Opacities[] =
	{
		0.5, 0.5208333, 0.5416666, 0.56249994, 0.58333325, 0.60416657,
		0.6249999, 0.6458332, 0.6666665, 0.6874998, 0.70833313, 0.72916645,
		0.74999976, 0.7708331, 0.7916664, 0.8124997, 0.833333
	};
	
	virtual void SceptrePuffStuff(bool hitThing)
	{
		let sceptreLevel = CallACS("GetSceptreLevel");
		
		if (dvdsdebug_showmeleeinformation)
			Console.Printf("Sceptre puff spawned. Sceptre level is %d.", sceptreLevel);
		
		A_PuffQuake(
			QuakeBaseIntensity[sceptreLevel],
			QuakeBaseDuration[sceptreLevel],
			QuakeBaseRadius[sceptreLevel]
		);
		
		A_PlaySound(
			hitThing?
			HitThingSounds[sceptreLevel] :
			HitWallSounds[sceptreLevel]
		);
		
		Scale.X = Scales[sceptreLevel];
		Scale.Y = Scale.X;
		Alpha = Opacities[sceptreLevel];
	}
	
	states
	{
		Spawn:
		Melee:
			TNT1 A 1;
			TNT1 A 0
			{
				SceptrePuffStuff(true);
				return ResolveState("Anim");
			}
		
		Crash:
			TNT1 A 1;
			TNT1 A 0
			{
				SceptrePuffStuff(false);
				return ResolveState("Anim");
			}
		
		Anim:
			SCPU CCDDDEEEFFFGGG 1 bright { Alpha -= Alpha / 5.; }
			stop;
	}
}

class SceptreShockwave : SceptrePuff
{
	static const float Scales[] =
	{
		1.125, 1.21875, 1.3125, 1.40625, 1.5, 1.59375,
		1.6875, 1.78125, 1.875, 1.96875, 2.0625, 2.15625,
		2.25, 2.34375, 2.4375, 2.53125, 2.625
	};
	
	override void SceptrePuffStuff(bool hitThing)
	{
		Super.SceptrePuffStuff(hitThing);
		
		// Override the scale for shockwaves.
		Scale.X = Scales[CallACS("GetSceptreLevel")];
		Scale.Y = Scale.X;
	}
	
	states
	{
		Anim:
			SCSK GHIJK 1 bright { Alpha -= Alpha / 7.; }
			stop;
	}
}
